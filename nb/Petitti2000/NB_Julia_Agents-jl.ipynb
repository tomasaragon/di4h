{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "6ced9aac",
   "metadata": {},
   "source": [
    "# Decision analysis with Julia `Agents.jl` using Petitti, 2000, 2e. - Chapter 2 \n",
    "Tomás Aragón, Updated 2026-01-21\n",
    "\n",
    "## Backgroud\n",
    "\n",
    "This Julia Jupyter Notebook demonstrates decision analysis using agent-based (ABM) modeling with the `Agents.jl` Julia package. The example is based on the decision tree from Diana B. Petitti. *Meta-Analysis, Decision Analysis, and Cost-Effectiveness Analysis: Methods for Quantitative Synthesis in Medicine*. 2nd ed. Monographs in Epidemiology and Biostatistics, v. 31. Oxford University Press, 2000. https://doi.org/10.1093/acprof:oso/9780195133646.001.0001.\n",
    "\n",
    "The ABM replicates these previous decision analyses:\n",
    "- DA using a decision tree (`rdecision`): [R Jupyter Notebook (Part 2)](https://github.com/tomasaragon/di4h/blob/main/nb/Petitti2000/NB_R_rdecision_part2.ipynb)   \n",
    "- DA using influence diagrams (`DecisionProgramming.jl`): [Julia Jupyter Notebook](https://github.com/tomasaragon/di4h/blob/main/nb/Petitti2000/NB_Julia_DecisionProgramming-jl.ipynb)\n",
    "\n",
    "The `Agents.jl` package documentation is here: https://juliadynamics.github.io/Agents.jl/stable/.\n",
    "\n",
    "To learn more about the Julia language visit: https://julialang.org ."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8305b31e",
   "metadata": {},
   "source": [
    "Here is the decision tree from Petitti, 2000, 2e, Chapter 2, Figure 2-6 (FIGURE 1). "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "09117bcc",
   "metadata": {},
   "source": [
    "<figure>\n",
    "<img src=\"Petitti_2000_2e_ch02_fig2-6.png\" width=\"800\" alt=\"Petitti 2000 Figure 2-6\"/>\n",
    "<figcaption>FIGURE 1. Decision tree from Petitti, 2000, 2e, Chapter 2, Figure 2-6.</figcaption>\n",
    "</figure>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f7b62e20",
   "metadata": {},
   "source": [
    "Now, here is the decision tree as an influence diagram (FIGURE 2). Notice there is no arrow from the measles node to the value node. The value node only contains input information on revaccination status and mortality based on the scenario. If measles morbidity had been included in the scenario then we would have added an arrow from the measles node to value node. I find influence diagrams more concise and interpretable. Because influence diagrams are directed acyclic graphs (DAGs), they promote causal reasoning. "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a902af1d",
   "metadata": {},
   "source": [
    "<figure>\n",
    "<img src=\"img_measles_decision_network_drawio.png\" width=\"600\" alt=\"Petitti 2000 Figure 2-1\"/>\n",
    "<figcaption>FIGURE 2: Influence diagram for Figure 2-6 from Petitti, 2000, 2e, Chapter 2.</figcaption> \n",
    "</figure>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e3b36cd3",
   "metadata": {},
   "source": [
    "We have a human agent that can experience these states: revaccination, exposure to measles, measles infection, and death from measles.  \n",
    "- $P(E = 1) = 0.20$\n",
    "- $P(M = 1 \\mid E = 1, R = 1) = 0.05$\n",
    "- $P(M = 1 \\mid E = 1, R = 0) = 0.33$\n",
    "- $P(D = 1 \\mid M = 1) = 0.0023$\n",
    "\n",
    "where: $E$ is exposure to measles $\\{1=yes, 0=no\\}$, $M$ is measles infection $\\{1=yes, 0=no\\}$, $D$ is death from measles $\\{1=yes, 0=no\\}$, and $R$ is revaccination status $\\{1=yes, 0=no\\}$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "290a424e",
   "metadata": {},
   "source": [
    "Steps to implement the agent-based model: \n",
    "1. create agent type\n",
    "2. create agent stepping function\n",
    "3. intitialize model: create model and add agents\n",
    "4. run simulation\n",
    "5. analyze results"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0f32ef25",
   "metadata": {},
   "source": [
    "## Julia code using Agents.jl"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e7b6dcf7",
   "metadata": {},
   "source": [
    "### Julia housekeeping\n",
    "- activate environment\n",
    "- ensure latest version of Agents.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "e231dbb7",
   "metadata": {},
   "outputs": [],
   "source": [
    "#import Pkg # Run to update Agents.jl to v6+\n",
    "## --- 1. Activate a new folder for this project\n",
    "#Pkg.activate(\"DAProject\");\n",
    "## --- 2. Force install the latest version\n",
    "#Pkg.update();\n",
    "## --- 3. Check the version (should be v6+)\n",
    "#Pkg.status(\"Agents\");"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cfb43d60",
   "metadata": {},
   "source": [
    "### Load packages and set constants"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "f9f8cb4f",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "0.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using Agents\n",
    "using Random\n",
    "using Statistics\n",
    "\n",
    "# Probabilities\n",
    "const p_E1 = 0.20          \n",
    "const p_M1_E1_R1 = 0.05    \n",
    "const p_M1_E1_R0 = 0.33    \n",
    "const p_D1_M1 = 0.0023     \n",
    "\n",
    "# Utilities\n",
    "const U_Alive = 1.0\n",
    "const U_Dead = 0.0"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7d78b3d4",
   "metadata": {},
   "source": [
    "### Step 1 - Create agent type\n",
    "Agents.jl provides the `@agent` macro that enables us to create an agent type quickly with minimal coding. It takes this basic informatioin and constructs a new data type with a variable for tracking unique IDs, etc. In this simulation we will create 1 million agents. Each agents has five mutuable attributes: revaccination, exposure, measles, death, and utility. Revaccination status will be set by the analyst (me), the others are stochastic. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "8d965b14",
   "metadata": {},
   "outputs": [],
   "source": [
    "@agent struct Patient(NoSpaceAgent)\n",
    "    revaccinate::Bool \n",
    "    exposed::Bool     \n",
    "    measles::Bool     \n",
    "    dead::Bool        \n",
    "    utility::Float64  \n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "80b22087",
   "metadata": {},
   "source": [
    "### Step 2 - Create agent stepping function\n",
    "Next, we need to create a function that captures the possible state changes and transition probabilities of the an agent. Fortunately, this is a simple example: revaccination, exposure, measles, and death."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "d58aaab5",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "agent_step! (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function agent_step!(agent::Patient, model)\n",
    "    # 1. Chance Node E: Exposure\n",
    "    # Use abmrng(model) to access the random number generator\n",
    "    rng = abmrng(model)\n",
    "    \n",
    "    if rand(rng) < p_E1\n",
    "        agent.exposed = true\n",
    "        \n",
    "        # 2. Chance Node M: Measles\n",
    "        prob_measles = agent.revaccinate ? p_M1_E1_R1 : p_M1_E1_R0\n",
    "        \n",
    "        if rand(rng) < prob_measles\n",
    "            agent.measles = true\n",
    "            \n",
    "            # 3. Chance Node D: Death\n",
    "            if rand(rng) < p_D1_M1\n",
    "                agent.dead = true\n",
    "                agent.utility = U_Dead\n",
    "            else\n",
    "                agent.dead = false\n",
    "                agent.utility = U_Alive\n",
    "            end\n",
    "        else\n",
    "            agent.measles = false\n",
    "            agent.utility = U_Alive\n",
    "        end\n",
    "    else\n",
    "        agent.exposed = false\n",
    "        agent.utility = U_Alive\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4ef1ec9f",
   "metadata": {},
   "source": [
    "### Step 3 - Intitialize model: create model and add agents\n",
    "At this step we create the model using the `StandardABM` function which, for our inputs, has 4 arguments:\n",
    "`StandardABM(::Type{A}, space::S; rng, agent_step!, ...)`. The arguments before the `;` are positional arguments and those after the `;` are keyword arguments. The first argument is the agent type we created in Step 1. The second argument is the space type. In this case we use `NoSpace()` because we do not need a spatial component. The third argument is the random number generator (RNG). If we do not provide one, a default RNG will be used. The fourth argument is the agent stepping function we created in Step 2. Use `methods(StandardABM)` to see other mthods for the `StandardABM` function.    \n",
    "\n",
    "Next, we add agents to the model using the `add_agent!` function. Arguments 2 to 6 are the agent attributes we created in Step 1. revaccinate, exposed, measles, dead, and utility. We will create 1 million agents with revaccination status set to true or false based on the scenario. The other attributes will be set to false or 0 initially."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "7b516a9e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "initialize_model (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function initialize_model(; n_agents = 100_000, decision_revaccinate = true)\n",
    "    space = nothing\n",
    "    # Create model with v6 syntax\n",
    "    model = StandardABM(Patient, space; rng = MersenneTwister(42), agent_step! = agent_step!)\n",
    "    \n",
    "    # Create agents\n",
    "    for i in 1:n_agents\n",
    "        add_agent!(model, decision_revaccinate, false, false, false, 0.0)\n",
    "    end\n",
    "    \n",
    "    return model\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "387d0c55",
   "metadata": {},
   "source": [
    "### Step 4 - Run simulation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "66c09fa9",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "--- Agents.jl Monte Carlo Results ---\n",
      "R1 (Revaccinate) Expected Utility: 0.999968\n",
      "R0 (No Revax)    Expected Utility: 0.999843\n"
     ]
    }
   ],
   "source": [
    "# --- Scenario 1: R1 (Revaccinate) ---\n",
    "model_R1 = initialize_model(n_agents = 1_000_000, decision_revaccinate = true)\n",
    "run!(model_R1, 1) \n",
    "\n",
    "eu_R1 = mean(model_R1[id].utility for id in allids(model_R1))\n",
    "\n",
    "# --- Scenario 2: R0 (Do Not Revaccinate) ---\n",
    "model_R0 = initialize_model(n_agents = 1_000_000, decision_revaccinate = false)\n",
    "run!(model_R0, 1) \n",
    "\n",
    "eu_R0 = mean(model_R0[id].utility for id in allids(model_R0))\n",
    "\n",
    "# --- Results ---\n",
    "println(\"--- Agents.jl Monte Carlo Results ---\")\n",
    "println(\"R1 (Revaccinate) Expected Utility: $eu_R1\")\n",
    "println(\"R0 (No Revax)    Expected Utility: $eu_R0\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a815308d",
   "metadata": {},
   "source": [
    "### Step 5 - Analyze results"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "4753c5a4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "0.000125"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# calculate risk difference as on pp. 27-28\n",
    "risk_death_no_revax = 1 - eu_R0\n",
    "risk_death_revax = 1 - eu_R1\n",
    "risk_difference = risk_death_no_revax - risk_death_revax\n",
    "round(risk_difference, digits = 6)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "30d6c027",
   "metadata": {},
   "source": [
    "From Petitti, pp. 27-28: \"The difference in the expected probability of death from measles between a strategy of revaccination and a strategy of no-revaccination is `0.000125`. This is interpreted to mean that 12.9 deaths from measles are prevented per 100,000 children revaccinated."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "60379946",
   "metadata": {},
   "source": [
    "## Appendix - Selected Julia resources\n",
    "- [The Julia Programming Language](https://julialang.org/) \n",
    "- [My journey from R to Julia: A very brief introduction to Julia for epidemiologists](https://tomasaragon.github.io/posts/2023-01-14-my-journey-from-r-to-julia/) (blog) \n",
    "- [Julia for Data Analysis: A book review for population health data scientists](https://tomasaragon.github.io/posts/2023-02-20-julia-for-data-analysis/) (blog)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "62d2a8de",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.3",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
